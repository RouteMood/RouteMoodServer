plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'nu.studer.jooq' version '9.0'
}

group = 'ru.hse.routemood'
version = '0.0.1-SNAPSHOT'

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // This dependency is used by the application.
    implementation libs.guava
    implementation 'com.google.code.gson:gson:2.12.1'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'


    implementation 'com.squareup.okhttp3:okhttp:3.14.6'
    implementation 'jakarta.persistence:jakarta.persistence-api'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    implementation 'commons-io:commons-io:2.6'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    implementation 'com.google.api-client:google-api-client:2.2.0'
    implementation 'com.google.api-client:google-api-client-jackson2:1.20.0'

    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    jooqGenerator 'org.postgresql:postgresql:42.7.3'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    jvmArgs = [
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "-javaagent:${classpath.find { it.name.contains("byte-buddy-agent") }.absolutePath}",
            "-XX:+EnableDynamicAgentLoading"
    ]
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jooq {
    version = '3.19.19'  // Используем актуальную версию

    configurations {
        main_jooq {
            generationTool {
                generator {
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = 'rating_item'

                        // Настройки именования
                        properties {
                            property {
                                key = 'unescapedNameCase'
                                value = 'as_is'
                            }
                        }

                        // Конвертеры для сложных типов
                        forcedTypes {
                            forcedType {
                                name = 'JSONB'
                                userType = 'java.util.List<java.lang.Double>'
                                converter = 'ru.hse.routemood.rating.converters.RouteConverter'
                                includeTypes = 'jsonb'
                            }
                        }
                    }

                    generate {
                        // Основные параметры генерации
                        deprecated = false
                        records = true
                        immutablePojos = false
                        fluentSetters = true

                        // Генерация POJO
                        pojos = true

                        // Использование Java 8+ типов
                        javaTimeTypes = true
                    }

                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            // Подключаем папку, в которую jOOQ пишет сгенерированное
            srcDir "$buildDir/generated-jooq"
        }
    }
}